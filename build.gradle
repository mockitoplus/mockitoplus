plugins {
    id 'jacoco'
    id 'java'
    id 'maven-publish'
    id "org.shipkit.java" version "2.3.5"
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2')
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile) {
  options.deprecation = true
  options.encoding = 'UTF-8'
  options.compilerArgs += ["-Xlint:all", "-Werror", "-Xdoclint:none"]
}

dependencies {
    implementation 'org.mockito:mockito-core:3.12.4'
    testImplementation 'org.assertj:assertj-core:3.21.0'
    testImplementation 'org.slf4j:slf4j-api:1.7.32'
    testImplementation 'org.slf4j:slf4j-simple:1.7.32'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
}

group = 'io.github.mockitoplus'
description = 'mockitoplus'

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

jacoco {
    toolVersion = "0.8.6"
}

test {
    useJUnitPlatform()
    failFast = true
    maxParallelForks = 4
}

jacocoTestReport {
    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.85
            }
        }
    }
}
