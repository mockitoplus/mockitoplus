buildscript {
  repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
  }

  dependencies {
        classpath 'org.shipkit:shipkit-changelog:1.2.0'
        classpath 'org.shipkit:shipkit-auto-version:1.2.2'
        classpath "io.github.gradle-nexus:publish-plugin:1.2.0"
    }
}

plugins {
    id 'jacoco'
    id 'java'
    id "java-library"
    id "maven-publish"
    id "org.openrewrite.rewrite" version "5.36.0"
}

apply from: 'gradle/shipkit.gradle'

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2')
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile) {
  options.deprecation = true
  options.encoding = 'UTF-8'
  options.compilerArgs += ["-Xlint:all", "-Werror"]
}

tasks.withType(Javadoc) {
  options.addStringOption('Xdoclint:none', '-quiet')
}

dependencies {
    implementation 'org.mockito:mockito-core:5.1.1'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.slf4j:slf4j-api:1.7.36'
    testImplementation 'org.slf4j:slf4j-simple:1.7.36'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

group = 'io.github.mockitoplus'
description = 'mockitoplus'

jacoco {
    toolVersion = "0.8.7"
}

test {
    useJUnitPlatform()
    failFast = true
    maxParallelForks = 5
}

jacocoTestReport {
    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.85
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
        }
    }
}

dependencies {
    rewrite(platform("org.openrewrite.recipe:rewrite-recipe-bom:1.14.0"))
    rewrite("org.openrewrite.recipe:rewrite-testing-frameworks")
    rewrite("org.openrewrite.recipe:rewrite-logging-frameworks")
}

rewrite {
    failOnDryRunResults = true
    activeRecipe("org.openrewrite.java.testing.junit5.JUnit5BestPractices")
    activeRecipe("org.openrewrite.java.logging.slf4j.Slf4jBestPractices")
}
