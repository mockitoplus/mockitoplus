buildscript {
  repositories {
        gradlePluginPortal()
  }

  dependencies {
        classpath 'org.shipkit:shipkit-changelog:2.0.1'
        classpath 'org.shipkit:shipkit-auto-version:2.0.5'
        classpath "io.github.gradle-nexus:publish-plugin:2.0.0-rc-2"
    }
}

plugins {
    id 'jacoco'
    id 'java'
    id "java-library"
    id "org.openrewrite.rewrite" version "6.10.0"
}

apply from: 'gradle/java-publication.gradle'
apply from: 'gradle/shipkit.gradle'

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

tasks.withType(JavaCompile) {
  options.deprecation = true
  options.encoding = 'UTF-8'
  options.compilerArgs += ["-Xlint:all", "-Werror"]
}

tasks.withType(Javadoc) {
  options.addStringOption('Xdoclint:none', '-quiet')
}

dependencies {
    implementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.assertj:assertj-core:3.25.3'
    testImplementation 'org.slf4j:slf4j-api:1.7.36'
    testImplementation 'org.slf4j:slf4j-simple:1.7.36'
    testImplementation(platform("org.junit:junit-bom:5.10.2"))
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation "org.junit.jupiter:junit-jupiter"
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    rewrite(platform("org.openrewrite.recipe:rewrite-recipe-bom:2.8.0"))
    rewrite("org.openrewrite.recipe:rewrite-testing-frameworks")
    rewrite("org.openrewrite.recipe:rewrite-logging-frameworks")
}

group = 'io.github.mockitoplus'
description = 'mockitoplus'

test {
    useJUnitPlatform()
    failFast = true
    maxParallelForks = 5
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.85
            }
        }
    }
}

rewrite {
    failOnDryRunResults = true
    activeRecipe("org.openrewrite.java.testing.junit5.JUnit5BestPractices")
    activeRecipe("org.openrewrite.java.logging.slf4j.Slf4jBestPractices")
}
